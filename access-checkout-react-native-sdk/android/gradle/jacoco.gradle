apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.7'
    reportsDirectory = layout.buildDirectory.dir('reports')
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

def codeCoverageExclusions = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*'
]

def classDirs = files([
        fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: codeCoverageExclusions) +
                fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: codeCoverageExclusions)
])

def execData = files("$buildDir/jacoco/testInstrumentedTestsDebugUnitTest.exec")

def sourceDirs = files(["${project.projectDir}/src/main/java"])

task testCoverage(type: JacocoReport, dependsOn: ['testInstrumentedTestsDebugUnitTest']) {
    executionData.setFrom(execData)
    classDirectories.setFrom(classDirs)
    sourceDirectories.setFrom(sourceDirs)

    reports {
        html.enabled true
    }
}
