buildscript {
    ext.getExtOrDefault = { name ->
        return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['Worldpay_' + name]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.2"
        // noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
    }
}


apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"

apply plugin: 'maven-publish'

apply from: './jacoco.gradle'
apply from: './publications.gradle'

def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Worldpay_" + name]).toInteger()
}

def supportsNamespace() {
    def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
    def major = parsed[0].toInteger()
    def minor = parsed[1].toInteger()

    // Namespace support was added in 7.3.0
    return (major == 7 && minor >= 3) || major >= 8
}

def isNewArchitectureEnabled() {
    // To opt-in for the New Architecture, you can either:
    // - Set `newArchEnabled` to true inside the `gradle.properties` file
    // - Invoke gradle with `-newArchEnabled=true`
    // - Set an environment variable `ORG_GRADLE_PROJECT_newArchEnabled=true`
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (isNewArchitectureEnabled()) {
    apply plugin: 'com.facebook.react'
}
android {

    if (supportsNamespace()) {
        namespace "com.worldpay.access.checkout.reactnative"

        sourceSets {
            main {
                manifest.srcFile "src/main/AndroidManifestNew.xml"
            }
        }
    }

    defaultConfig {
        compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")
        minSdkVersion getExtOrIntegerDefault("minSdkVersion")
        targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GradleCompatible"
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests {
            includeAndroidResources = true
        }
    }
}

repositories {
    mavenCentral()
    google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")


dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation 'com.facebook.react:react-android:0.73.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.worldpay.access:access-checkout-android:4.2.1'

    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'org.mockito:mockito-core:5.16.1'


    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android'

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.awaitility:awaitility-kotlin:3.1.6'
    androidTestImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'

    // Testing-only dependencies
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation "androidx.test.ext:junit:1.2.1";
    androidTestImplementation "androidx.test:runner:1.6.1"
    androidTestImplementation 'com.google.android.gms:play-services-vision:20.1.3'
    androidTestUtil 'androidx.test:orchestrator:1.5.1'

    // noinspection GradleDependency
    androidTestImplementation 'org.assertj:assertj-core:3.26.3'
    androidTestImplementation "com.github.tomakehurst:wiremock-standalone:2.27.2"
    androidTestImplementation 'org.mockito:mockito-android:4.3.1'
}
